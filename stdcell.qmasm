###################################
# QMASM standard-cell library     #
# for use with edif2qmasm         #
#                                 #
# By Scott Pakin <pakin@lanl.gov> #
###################################

# N.B. Weights and strengths are currently scaled so that the maximum
# absolute value is 1.0.  This may change in a future version of this
# library.

# Y = A AND B
!begin_macro AND
  A -0.5
  B -0.5
  Y  1

  A B  0.5
  A Y -1
  B Y -1
!end_macro AND

# Y = A OR B
!begin_macro OR
  A  0.5
  B  0.5
  Y -1

  A B  0.5
  A Y -1
  B Y -1
!end_macro OR

# Y = NOT A
!begin_macro NOT
  A Y 1.0
!end_macro NOT

# Y = A XOR B
!begin_macro XOR
  A    0.5
  B   -0.5
  Y   -0.5
  $a1  1

  A B   -0.5
  A Y   -0.5
  A $a1  1
  B Y    0.5
  B $a1 -1
  Y $a1 -1
!end_macro XOR

# Y = S ? B : A
!begin_macro MUX
  S    0.5
  A    0.25
  B   -0.25
  Y    0.5
  $a1  1

  S A   0.25
  S B   -0.25
  S Y    0.5
  S $a1  1
  A B    0.5
  A Y   -0.5
  A $a1  0.5
  B Y   -1
  B $a1 -0.5
  Y $a1  1
!end_macro MUX

# Q = D (clock is ignored)
!begin_macro DFF_P
  Q = D
!end_macro DFF_P

# Q = D (clock is ignored)
!begin_macro DFF_N
  Q = D
!end_macro DFF_N

# Constants for power and ground.
!alias VCC true
!alias GND false
